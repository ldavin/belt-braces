buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "org.jetbrains.dokka" version "0.9.17"
    id "maven-publish"
    id "jacoco"
}

group 'io.ldavin'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:2.4.1'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = 'Belt & Braces'
                description = 'A time-saver when adding tests on existing code'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'ldavin'
                        name = 'Louis Davin'
                        email = 'louis.davin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ldavin/belt-braces.git'
                    developerConnection = 'scm:git:ssh://github.com/ldavin/belt-braces.git'
                    url = 'https://github.com/ldavin/belt-braces'
                }
            }
        }
    }
}