plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "org.jetbrains.dokka" version "0.9.18"
    id "maven-publish"
    id "jacoco"
}

group 'io.ldavin'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    testImplementation "junit:junit:4.12"
    testImplementation "org.assertj:assertj-core:3.15.0"
    testImplementation "org.mockito:mockito-core:2.27.0"
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    testImplementation "eu.codearte.catch-exception:catch-exception:2.0"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
project.tasks.check.dependsOn(jacocoTestReport)

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = 'Belt & Braces'
                description = 'A time-saver when adding tests on existing code'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'ldavin'
                        name = 'Louis Davin'
                        email = 'louis.davin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ldavin/belt-braces.git'
                    developerConnection = 'scm:git:ssh://github.com/ldavin/belt-braces.git'
                    url = 'https://github.com/ldavin/belt-braces'
                }
            }
        }
    }
}